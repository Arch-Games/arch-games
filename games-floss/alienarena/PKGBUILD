# Contributor: Paul Bredbury <brebs@sent.com>

pkgname=alienarena
pkgver=20080603
pkgrel=1
pkgdesc="Multiplayer retro sci-fi deathmatch game"
arch=(i686 x86_64)
url="http://red.planetarena.org/"
license=('GPL')
depends=('curl' 'libgl' 'libjpeg' 'libxxf86dga' 'libxxf86vm' 'mesa' 'sdl')
makedepends=('hd2u' 'p7zip' 'unzip')
source=(http://alienarena.arcadebelgium.be/alienarena2008-linux${pkgver}.zip
        http://forsakenweb.com/alienarena/files/accessorypack2008.7z
        alienarena.desktop)
md5sums=('f05f373ef438286306c1bc90642e54bf'
         '0e47e4c72ef11a34e598f7acd135660f'
         '46bbe3e3a93b2e5009ffd663ad462915')

_datadir="/usr/share/${pkgname}"
_libdir="/usr/lib/${pkgname}"

build() {
  cd $srcdir/${pkgname}2008

  # Additional maps & skins
  7z x -y ../accessorypack2008.7z || return 1

  # Fix filenames
  mv "data1/scripts/maps/tca-titan2k8 .rscript" data1/scripts/maps/tca-titan2k8.rscript

  # Remove pre-compiled libs
  rm {arena,data1}/game.so

  cd source
  # Arch isn't multilib
  sed -i -e "s:lib64:lib:" Makefile || return 1

  make PREFIX=/usr \
       WITH_DATADIR=yes WITH_LIBDIR=yes \
       DATADIR="${_datadir}" LIBDIR="${_libdir}" \
       || return 1

  cd release
  # opengl seems more stable, but may vary
  install -D -m755 game.so $pkgdir/${_libdir}/arena/game.so || return 1
  install -D -m755 crx $pkgdir/usr/bin/${pkgname} || return 1
  install -D -m755 crx.sdl $pkgdir/usr/bin/${pkgname}-sdl || return 1
  install -D -m755 crded $pkgdir/usr/bin/${pkgname}-ded || return 1

  cd ../..
  # Data files (can contain spaces)
  find arena botinfo data1 -type f | while read _f ; do
    install -D -m644 "$_f" "$pkgdir/$_datadir/$_f" || return 1
  done

  # Desktop entry
  install -D -m644 aa.png $pkgdir/usr/share/pixmaps/${pkgname}.png || return 1
  install -D -m644 $srcdir/${pkgname}.desktop $pkgdir/usr/share/applications/${pkgname}.desktop || return 1

  # Docs
  cd docs
  dos2unix README.txt
  mkdir -p $pkgdir/usr/share/doc/$pkgname
  install -m644 -t $pkgdir/usr/share/doc/${pkgname}/ {license,README}.txt || return 1
}
