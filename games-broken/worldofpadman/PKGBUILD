# Contributor: Paul Bredbury <brebs@sent.com>
# Contributor: Benjamin Dirks <asaru[at]wtnet[dot]de>

pkgname=worldofpadman
pkgver=1.2
pkgrel=2
pkgdesc="Cartoon-style multiplayer first-person shooter"
arch=(i686 x86_64)
url="http://www.worldofpadman.com/"
license=('custom')
depends=('curl' 'libogg' 'libvorbis' 'mesa' 'openal' 'sdl')
makedepends=('unzip')
# Based on http://sources.gentoo.org/viewcvs.py/gentoo-x86/games-fps/worldofpadman/
source=(http://thilo.kickchat.com/download/worldofpadman.run
        http://distfiles.gentoo.org/distfiles/wop_padpack.zip
        http://distfiles.gentoo.org/distfiles/wop-engine-1.2.tar.bz2
        http://distfiles.gentoo.org/distfiles/wop_patch_1_2.run
        worldofpadman.desktop)
_gamedir="/usr/share/${pkgname}"
md5sums=('24d58ec4b8231ec4815af51814f1276a' 'c59e212b29b28e0162af33dfb118344c' '9c30e87859da61567213868b1e52d340' '3468fc91889795471bc68e35ea334614' 'c5bc8f05859eaa2babfb7683f4c68dab')

build() {
  cd ${startdir}/src
  # Based on http://sources.gentoo.org/viewcvs.py/gentoo-x86/eclass/eutils.eclass
  for _f in {worldofpadman,wop_patch_1_2}.run ; do
    _skip_lines=$(grep -a offset=.*head.*wc $_f | awk '{print $3}' | head -n 1)
    _skip_chars=$(head -n ${_skip_lines} $_f | wc -c)
    dd ibs=${_skip_chars} skip=1 obs=1024 conv=sync if=$_f | tar --no-same-owner -xzf - || return 1
  done
  tar -xf readme.tar || return 1
  mkdir wop
  cd wop
  tar -xf ../wop-data.tar || return 1
  tar -xf ../wop-data-${pkgver}.tar || return 1
  tar -xf ../extras.tar || return 1
  unzip -qo ../wop_padpack.zip || return 1
  mv PadPack.txt ..
  # Compile
  cd ${startdir}/src/wop-engine-${pkgver}
  make BUILD_CLIENT=1 BUILD_SERVER=1 DEFAULT_BASEDIR="${_gamedir}" || return 1
  # Executables
  cd build/release-*
  install -D -m755 wop-engine.* $startdir/pkg/usr/bin/${pkgname} || return 1
  install -D -m755 wopded.* $startdir/pkg/usr/bin/${pkgname}-ded || return 1
  # Data
  cd ${startdir}/src
  # Using "read", so can handle filenames containing spaces
  find wop -type f | while read _f ; do
    install -D -m644 "$_f" "$startdir/pkg/$_gamedir/$_f" || return 1
  done
  # Return from the function, since install's return just exits the loop
  # See http://fvue.nl/wiki/Bash:_Error_handling
  [ $? -gt 0 ] && return 1
  # Desktop
  install -D -m644 $startdir/src/wop.png $startdir/pkg/usr/share/pixmaps/$pkgname.png || return 1
  install -D -m644 $startdir/src/$pkgname.desktop $startdir/pkg/usr/share/applications/$pkgname.desktop || return 1
  # Docs
  cd ${startdir}/src
  # Using "read", so can handle filenames containing spaces
  find readme -type f | while read _f ; do
    install -D -m644 "$_f" "$startdir/pkg/usr/share/doc/$pkgname/$_f" || return 1
  done
  # Return from the function, since install's return just exits the loop
  # See http://fvue.nl/wiki/Bash:_Error_handling
  [ $? -gt 0 ] && return 1
  install -m644 -t $startdir/pkg/usr/share/doc/$pkgname/ PadPack.txt readme.html wop_patch_*.txt || return 1
  # License
  install -D -m644 $startdir/src/copyright_en.txt $startdir/pkg/usr/share/licenses/$pkgname/COPYING || return 1
}
