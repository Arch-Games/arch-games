# Contributor: rabyte <rabyte__gmail>

pkgname=openarena
pkgver=0.8.1
pkgrel=1
pkgdesc="A completely free multiplayer FPS game based on the ioquake3 engine"
url="http://openarena.ws/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('openal' 'sdl' 'libvorbis' 'curl')
makedepends=('unzip')
source=(http://download.tuxfamily.org/openarena/rel/081/oa081.zip
        http://openarena.ws/svn/source/081/$pkgname-engine-$pkgver-1.tar.bz2
        openarena.png
        openarena.desktop
        openarena.sh
        oa_ded.sh)

md5sums=('49006bcb02b4e8ea3d06749e8f4e4887'
         '4ee696eacc4b0350f9dbb5588dcd74a2'
         'c69f824dc500d436fc631b78b6a62dc8'
         '8ca6da826f35857ea1b3f4f17734843d'
         '02e47aac0a157f07c748818afac22e5e'
         'af298f15420dea2bf003489ac9bf825f')

build() {

  #Build the package
  cd $srcdir/$pkgname-engine-$pkgver
  make  DEFAULT_BASEDIR=/opt/openarena BUILD_CLIENT_SMP=0 USE_LOCAL_HEADERS=0 GENERATE_DEPENDENCIES=0 || return 1
  make COPYDIR=$pkgdir/opt/openarena copyfiles || return 1

  #Fix the stupid naming conversion
  mv $pkgdir/opt/openarena/openarena* $pkgdir/opt/openarena/openarena
  mv $pkgdir/opt/openarena/oa_ded* $pkgdir/opt/openarena/oa_ded

  #Manually install the package
  install -D -m644 $srcdir/$pkgname-$pkgver/baseoa/* $pkgdir/opt/openarena/baseoa/
  install -D -m644 $srcdir/$pkgname-$pkgver/missionpack/* $pkgdir/opt/openarena/missionpack/

  #Install the .desktop and icon files
  install -D -m644 $srcdir/openarena.png $pkgdir/usr/share/pixmaps/openarena.png
  install -D -m644 $srcdir/openarena.desktop $pkgdir/usr/share/applications/openarena.desktop

  #Install the bin files
  install -D -m755 $srcdir/openarena.sh $pkgdir/usr/bin/openarena
  install -D -m755 $srcdir/oa_ded.sh $pkgdir/usr/bin/oa_ded

}

# vim:set ts=2 sw=2 et:
