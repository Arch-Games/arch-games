#Contributor: Daenyth

pkgname=tremulous-updated
pkgver=1.1.0
pkgrel=1
pkgdesc="A free team based FPS/RTS hybrid built on the ioq3 engine. Includes community updates."
url="http://tremulous.net"
arch=('i686' 'x86_64')
license=('GPL')
depends=('sdl' 'openal')
provides=('tremulous=1.1.0')
conflicts=('tremulous')
replaces=('trem-backport')
source=(http://downloads.sourceforge.net/sourceforge/tremulous/tremulous-1.1.0.zip
        http://releases.mercenariesguild.net/client/mg-client-manual.txt
        http://releases.mercenariesguild.net/client/mgclient_source_1.0.tar.gz
        http://releases.mercenariesguild.net/tremded/mg_tremded_source_1.01.tar.gz
        game.qvm::http://patches.mercenariesguild.net/index.php?getfile=816
        lakitu7_qvm.txt::http://patches.mercenariesguild.net/index.php?getfile=812
        tremdedrc
        tremulous.desktop
        tremded.sh
        tremulous.sh)

noextract=(mg_tremded_source_1.01.tar.gz
           mgclient_source_1.0.tar.gz)

md5sums=('3df5f7565571fb9524656308347bce1b'
         '54dc412c71ee30d801d3ef53bfa89f7d'
         'a17a2b8f2d3fd9f0d136fc357e285b86'
         '938bdf944dff667b74e2132a87a49780'
         '5deb3bc2182aa22e121547b8af3d8fdb'
         'da75a281104e79e16ebfef69b415cecb'
         'f0056120d0192a0d4d591d1114439c52'
         'aef37632a2edcf74a53503a49530bad2'
         '63f49f4f177f265c4b90d0ca433f07c3'
         '02b899ce7691637705eb86480155c144')

build() {
  local _SUF=x86
  [ "$CARCH" = "x86_64" ] && _SUF=$CARCH

  # Build and install the server
  bsdtar -x -C $srcdir/tremded -f $srcdir/mg_tremded_source_1.01.tar.gz
  cd $srcdir/tremded
  make || return 1
  install -D -m755 build/release-linux-x86/tremded.$_SUF $pkgdir/opt/tremulous/tremded.$_SUF
  install -D -m644 $srcdir/tremdedrc                     $pkgdir/etc/tremdedrc
  install -D -m644 $srcdir/game.qvm                      $pkgdir/opt/tremulous/game.qvm
  install -D -m755 $srcdir/tremded.sh                    $pkgdir/usr/bin/tremded

  # Build and install the client
  bsdtar -x -C $srcdir/mgclient -f $srcdir/mgclient_source_1.0.tar.gz
  cd $srcdir/mgclient
  make || return 1
  install -Dm755 build/release-linux-x86/tremulous.$_SUF $pkgdir/opt/tremulous/tremulous.$_SUF
  install -D -m755 $srcdir/tremulous.sh                  $pkgdir/usr/bin/tremulous

  # Install required shared data
  cd $srcdir/tremulous/base
  install -d -m1775 -g games $pkgdir/opt/tremulous/base
  install -D -m644 *.pk3 *.cfg $pkgdir/opt/tremulous/base/

  # Install the documentation
  install -Dm644 $srcdir/tremulous/mg-client-manual.txt $pkgdir/usr/share/tremulous/mg-client-manual.txt
  install -Dm644 $srcdir/tremulous/lakitu7_qvm.txt      $pkgdir/usr/share/tremulous/lakitu7_qvm.txt

  # Install the .desktop and icon files
  install -D -m644 $srcdir/tremulous/tremulous.xpm $pkgdir/usr/share/pixmaps/tremulous.xpm
  install -D -m644 $srcdir/tremulous.desktop       $pkgdir/usr/share/applications/tremulous.desktop

}

# vim:set ts=2 sw=2 et:
