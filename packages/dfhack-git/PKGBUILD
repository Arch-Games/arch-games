# Contributor: Daenyth <Daenyth+Arch AT gmail DOT com>
pkgname=dfhack-git
pkgver=20100508
pkgrel=1
pkgdesc="A cross-platform library for accessing Dwarf Fortress memory"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/dfhack/"
license=('custom:zlib')
depends=(gcc-libs libx11)
makedepends=(cmake git)
conflicts=(dfhack dfhack-svn)
provides=(dfhack)
replaces=(dfhack-svn)
source=(dwarffortress-hacked)
md5sums=('779e8477b41e360556502cee984f8970')

_gitroot="git://github.com/peterix/dfhack.git"
_gitname="dfhack"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
  #
  # BUILD
  #

  cd build
  cmake .. -DCMAKE_BUILD_TYPE:string=Release -DCMAKE_INSTALL_PREFIX=$pkgdir/usr -DMEMXML_DATA_PATH:path=/usr/share/dfhack|| return 1
  # cmake .. -DCMAKE_BUILD_TYPE:string=Debug -DCMAKE_INSTALL_PREFIX=$pkgdir/usr -DMEMXML_DATA_PATH:path=/usr/share/dfhack|| return 1
  make || return 1
  make install

  cd ..
  install -Dm755 output/libdfconnect.so "$pkgdir/usr/lib/libdfconnect.so"
  install -Dm755 "$srcdir/dwarffortress-hacked" "$pkgdir/usr/bin/dwarffortress-hacked"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/dfhack/LICENCE"
}

# vim:set ts=2 sw=2 et:
