# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve> 

pkgname=fretsonfire
pkgver=1.3.110
pkgrel=1
pkgdesc="A game of musical skill and fast fingers. Play guitar with the keyboard as accurately as possible. Like guitar Hero."
url="http://fretsonfire.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('sdl' 'sdl_ttf' 'sdl_mixer' 'python-pygame' 'python-opengl' 'pil' 'python-numpy')
optdepends=('glew: Used for render-to-texture support' 'pyogg: Used for streaming OGG files from the disk')
[ "$CARCH" = "i686" ] && optdepends[3]='psyco: Used for speed optimization'
source=(http://downloads.sourceforge.net/fretsonfire/FretsOnFire-$pkgver.tar.gz
        fretsonfire.desktop
        fretsonfire.sh
        fretsonfire.png)
    
md5sums=('daa7e23ebc8ed5bc78aa5e9f4ca3471e'
         'e792e7dfe01775c0034781393c457696' 
         'ae24d6024235ec8ec92b8a391e675a03' 
         '66668cad0211bf4e6ab1bd5efa8f65bf')

build() {

  #Installing The files
  cd $srcdir/Frets\ on\ Fire-$pkgver
  install -d $pkgdir/usr/share/fretsonfire/bin $pkgdir/usr/share/fretsonfire/data || return 1
  cp -r src/* $pkgdir/usr/share/fretsonfire/bin/ || return 1
  cp -r data/* $pkgdir/usr/share/fretsonfire/data/ || return 1
  
  #Removing junk and setting permissions
  cd $pkgdir/usr/share/fretsonfire/data
  rm -r win32/ launcher* Makefile *.svg songs/tutorial/*.svg mods/Chilly/*.svg mods/LightGraphics/*.svg 
  find $pkgdir/usr/share/fretsonfire -type f -exec chmod 664 {} + 
  find $pkgdir/usr/share/fretsonfire -type d -exec chmod 775 {} + 
  
  #Installing the .desktop and icon files
  install -D $srcdir/fretsonfire.desktop  $pkgdir/usr/share/applications/fretsonfire.desktop
  install -D $srcdir/fretsonfire.png $pkgdir/usr/share/pixmaps/fretsonfire.png
  
  #Installing the bin file
  install -D -m0755 $srcdir/fretsonfire.sh $pkgdir/usr/bin/fretsonfire
    
}

# vim:set ts=2 sw=2 et:
