# Contributor: Travis Nickles <ryoohki7@yahoo.com>
# Contributor: Slash <demodevil5[at]yahoo[dot]com>

pkgname=enemy-territory
pkgver=2.60b
pkgrel=8
pkgdesc="Wolfenstein: Enemy Territory is a completely free, standalone, team-based, multiplayer FPS."
url="http://www.splashdamage.com/node/57"
arch=('i686' 'x86_64')
license=('custom')
depends=('mesa' 'libxext')
[ "$CARCH" = "x86_64" ] && depends=('lib32-mesa' 'lib32-libxext')
makedepends=('unzip')
optdepends=('enemy-territory-etpro (A Mod for Enemy Territory)')
install=enemy-territory.install 

backup=('opt/enemy-territory/etmain/server.cfg'
    'opt/enemy-territory/etmain/punkbuster.cfg')
  
source=(http://ftp.games.skynet.be/pub/wolfenstein/et-linux-2.60.x86.run 
        http://ftp.gwdg.de/pub/misc/ftp.idsoftware.com/idstuff/et/ET-$pkgver.zip 
        enemy-territory.desktop 
        et-ded 
        et-pbupdate.sh 
        et.sh 
        etded.sh)
    
md5sums=('2d2373f29f02e18d365d7f1860eee435' 
         'fb83b8732fc7373c277180b663debf57' 
         'c0f9fc3f6d77814dae435be2aa42c46e' 
         '665f288d654758343f11898031efeda5' 
         'ce374bdbae3c8e4f4319b37984a7d9e2' 
         'd55fc4880bcb2a93d2294b8861a852df' 
         '0645baca975faa176a388e8c41a9ff8d')

build() {
  
  #Install the game
  cd $srcdir
  chmod +x et-linux-2.60.x86.run
  ./et-linux-2.60.x86.run --noexec --target $pkgdir/opt/enemy-territory || return 1
  rm -r $pkgdir/opt/enemy-territory/bin $pkgdir/opt/enemy-territory/setup.{data,sh} $pkgdir/opt/enemy-territory/README
  
  #Apply patched executables
  cp Enemy\ Territory\ $pkgver/linux/{et,etded}.x86 $pkgdir/opt/enemy-territory

  #Install PunkBuster updater script
  find $pkgdir/opt/enemy-territory/pb -type f -exec chmod 660 {} +
  find $pkgdir/opt/enemy-territory/pb -type d -exec chmod 770 {} +
  chmod 770 $pkgdir/opt/enemy-territory/pb/pbweb.x86
  install -d -m770 $pkgdir/opt/enemy-territory/pb/{dll,svlogs,svss}
  chown -R :games $pkgdir/opt/enemy-territory/pb
  
  #Install rc.d script
  install -D -m755 et-ded $pkgdir/etc/rc.d/et-ded

  #Install desktop file and icon
  install -D -m644 enemy-territory.desktop $pkgdir/usr/share/applications/enemy-territory.desktop
  install -D -m644 $pkgdir/opt/enemy-territory/ET.xpm $pkgdir/usr/share/pixmaps/enemy-territory.xpm
  
  #Install licenses
  install -D -m644 $pkgdir/opt/enemy-territory/Docs/EULA_Wolfenstein_Enemy_Territory.txt $pkgdir/usr/share/licenses/$pkgname/EULA_Wolfenstein_Enemy_Territory
  install -D -m644 $pkgdir/opt/enemy-territory/pb/PB_EULA.txt $pkgdir/usr/share/licenses/$pkgname/PB_EULA.txt
  
  #Install the bin files
  install -D -m755 et.sh $pkgdir/usr/bin/enemy-territory
  install -D -m755 etded.sh $pkgdir/usr/bin/enemy-territory-server
  install -D -m755 et-pbupdate.sh $pkgdir/usr/bin/enemy-territory-pb-update
 
  #If arch is x86_64 fix the bin files to include lib32 dir
  if [ "$CARCH" = "x86_64" ] ; then
    sed "5,5c\export LD_LIBRARY_PATH=/opt/lib32/usr/lib/:/opt/lib32/lib/:\$LD_LIBRARY_PATH" -i $pkgdir/usr/bin/enemy-territory
    sed "5,5c\export LD_LIBRARY_PATH=/opt/lib32/usr/lib/:/opt/lib32/lib/:\$LD_LIBRARY_PATH" -i $pkgdir/usr/bin/enemy-territory-server
  fi
  
}

# vim:set ts=2 sw=2 et:
