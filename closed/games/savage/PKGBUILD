# Contributor: Slash <demodevil5[at]yahoo[dot]com>

pkgname=savage
pkgver=2.00e
pkgrel=7
pkgdesc="An online multiplayer team-based FPS/RTS hybrid. Completely free as of September 2006. (Includes SFE Patches)"
url="http://s2games.com/savage"
arch=('i686' 'x86_64')
license=('custom')
depends=('sdl' 'mesa' 'libjpeg' 'e2fsprogs')
[ "$CARCH" = "x86_64" ] && depends=('lib32-sdl' 'lib32-mesa' 'lib32-libjpeg' 'lib32-e2fsprogs' 'lib32-glib2' 'lib32-pcre' 'lib32-zlib' 'lib32-freetype2' 'lib32-libxdamage')
makedepends=('unzip')
install=savage.install
source=(savage.sh
        savage.desktop
        icon.xpm
        http://mnky.newerth.com/lin-client-auth-patch.zip
        http://www.notforidiots.com/SFE/SFE-Standalone.tar.gz)
    
md5sums=('1588610be6fe7c52eda37fee65ca16c4' 
         '1fcb84c031b22b73fd803a6e2a611aca' 
         'a73f68e3620382860b08e88c1125f55d' 
         '7fb1e0b31e838ea335a35c769c4549d8' 
         '3ce0c50e3d5e4ff6789122c5076fb66b')

build() {

  #Install the game
  cd $srcdir
  install -d $pkgdir/opt/savage
  tar zxf $srcdir/SFE-Standalone.tar.gz -C $pkgdir/opt/savage/
  
  #Install the client patch
  install -m 755 $srcdir/lin-client-auth-patch/silverback.bin $pkgdir/opt/savage/silverback.bin
  install -m 755 $srcdir/lin-client-auth-patch/game/game.so $pkgdir/opt/savage/game/game.so
  install -m 755 $srcdir/lin-client-auth-patch/libs/libpng12.so.0 $pkgdir/opt/savage/libs/libpng12.so.0
  
  #Create a log file
  touch $pkgdir/opt/savage/scripts.log
  
  #Set ownership to the games group
  chown -R root:games $pkgdir/opt/savage
  chmod -R g+w $pkgdir/opt/savage
  
  #Install a .desktop and icon file
  install -D -m 644 $srcdir/icon.xpm $pkgdir/usr/share/pixmaps/savage.xpm
  install -D -m 644 $srcdir/savage.desktop $pkgdir/usr/share/applications/savage.desktop
  
  #Install the license
  install -D -m 644 $srcdir/licenses.txt $pkgdir/usr/share/licenses/$pkgname/licenses.txt
  
  #Install the launcher
  install -D -m 755 $srcdir/savage.sh $pkgdir/usr/bin/savage
  
  #If arch is x86_64 fix the launcher to include lib32 dir
  if [ "$CARCH" = "x86_64" ] ; then
    sed "4,4c\export LD_LIBRARY_PATH=libs:/opt/lib32/usr/lib/:/opt/lib32/lib/:\$LD_LIBRARY_PATH" -i $pkgdir/usr/bin/savage
  fi
  
}

# vim:set ts=2 sw=2 et:
