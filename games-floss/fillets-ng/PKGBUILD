# Contributor: Dragonlord <archlinux[at]dragonlord[.]cz>

pkgname=fillets-ng
pkgver=0.8.0
pkgrel=5
pkgdesc="A port of the wonderful puzzle game Fish Fillets."
url="http://fillets.sf.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('lua' 'fribidi' 'sdl_mixer' 'sdl_ttf' 'sdl_image' 'gcc-libs' 'desktop-file-utils')
makedepends=('patch')
install=$pkgname.install
source=(http://downloads.sourceforge.net/fillets/$pkgname-$pkgver.tar.gz \
        http://downloads.sourceforge.net/fillets/$pkgname-data-$pkgver.tar.gz \
        $pkgname.desktop \
        $pkgname-$pkgver-gcc.patch)

md5sums=('5daccbed195ae74cdfa54755ef62e3ce'
         'dd43ff637bdc1e4e7445b35ce0a1aa1c'
         'cf077c4b770868485e46d67e5b0b6de8'
         '803ab9381984ebd0699f76ba7dfd9f01')

build() {
  cd "$startdir/src/$pkgname-$pkgver"

  patch -Np0 -i $startdir/src/$pkgname-$pkgver-gcc.patch

  ./configure --prefix=/usr --datadir=/usr/share/fillets-ng
  make || return 1
  make DESTDIR="$startdir/pkg" install

  install -d -m755 "$startdir/pkg/usr/share/fillets-ng"
  cp -r $startdir/src/$pkgname-data-$pkgver/* \
    $startdir/pkg/usr/share/fillets-ng || return 1

  install -D -m644 "$startdir/src/$pkgname-data-$pkgver/images/icon.png" \
    "$startdir/pkg/usr/share/pixmaps/fillets.png" || return 1
  install -D -m644 "$startdir/src/fillets-ng.desktop" \
    "$startdir/pkg/usr/share/applications/fillets-ng.desktop" || return 1
}
