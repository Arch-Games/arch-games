# Contributor: Eric Belanger <belanger@astro.umontreal.ca>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=maniadrive
pkgver=1.2
pkgrel=8
pkgdesc="Arcade car game on acrobatic tracks with quick and nervous gameplay"
arch=(i686)
url="http://maniadrive.raydium.org/"
license=('GPL' 'custom:CC by-sa 2.0')
depends=('alsa-lib' 'libgl' 'mesa')
source=(http://dl.sourceforge.net/sourceforge/${pkgname}/ManiaDrive-${pkgver}-linux-i386.tar.gz \
        maniadrive.png \
        maniadrive.desktop \
        license.txt)
md5sums=('5592eaf4b8c4012edcd4f0fc6e54c09c' 'c050f721db74ab131f8f92eec2d4a50b'\
         '9e901db6d128953ae4392600794c6b70' 'afa5fac56d01430e904dd6716d84f4bf')

build() {
  cd $srcdir/ManiaDrive-${pkgver}-linux-i386/

  # Fix paths in scripts
  sed -e 's|game|/opt/maniadrive|' -i *.sh

  # Install scripts (without the .sh extension)
  for _script in *.sh; do
    install -D -m755 ${_script} $pkgdir/usr/bin/${_script%.sh}
  done

  # Install data files
  mkdir -p $pkgdir/opt/${pkgname}
  mv game/* $pkgdir/opt/${pkgname}/

  # Install icon and shortcut
  install -D -m644 $srcdir/${pkgname}.png $pkgdir/usr/share/pixmaps/${pkgname}.png
  install -D -m644 $srcdir/${pkgname}.desktop $pkgdir/usr/share/applications/${pkgname}.desktop

  # Install Creative Commons license
  install -D -m644 $srcdir/license.txt $pkgdir/usr/share/licenses/${pkgname}/license.txt
}
