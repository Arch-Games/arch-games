# Contributor: Daenyth <Daenyth+Arch AT gmail DOT com>
pkgname=dwarftherapist-svn
pkgver=344
pkgrel=1
pkgdesc="An extension application for the fantastic game Dwarf Fortress"
arch=('i686' 'x86_64')
url="http://code.google.com/p/dwarftherapist/"
license=(MIT)
depends=(qt)
makedepends=(subversion)
source=(fix_file_path.diff linux-qmake-update.diff http://www.filebin.org/upload/updated_offsets_d17.zip)
md5sums=('5d9e493dcf6cf686e5d5d7f14db76879'
         '1d162e2bd25685ee6b98ad25b965fad8'
         '0b5934482ca7e45bf6d9388f5fe7e772')

_svntrunk=http://dwarftherapist.googlecode.com/svn/trunk/
_svnmod=${pkgname%-svn}-read-only

build() {
  cd "$srcdir" #/DwarfTherapist_LINUX_$pkgver"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  # Change stupid hardcoded path to slightly less stupid hardcoded path.
  patch -Np1 < "$srcdir/fix_file_path.diff" || return 1

  # Fix r344 to compile
  patch -Np1 < "$srcdir/linux-qmake-update.diff" || return 1

  # Build
  qmake dwarftherapist.pro || return 1
  make || return 1

  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/dwarftherapist/LICENSE"
  install -Dm755 DwarfTherapist "$pkgdir/usr/bin/dwarftherapist"
  install -Dm644 "$srcdir/etc/game_data.ini" "$pkgdir/usr/share/dwarftherapist/game_data.ini"
  install -dm755 "$pkgdir/usr/share/dwarftherapist/memory_layouts/linux/"
  install -Dm644 etc/memory_layouts/linux/*.ini "$pkgdir/usr/share/dwarftherapist/memory_layouts/linux/"
  install -Dm644 "$srcdir"/etc/memory_layouts/linux/*.ini "$pkgdir/usr/share/dwarftherapist/memory_layouts/linux/"
}

# vim:set ts=2 sw=2 et:
