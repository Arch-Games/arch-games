# Maintainer:  TDY <tdy@gmx.com>
# Contributor: Nelson Menon <nelsonmenon@gmail.com insanatorium.wordpress.com>

pkgname=bloboats
pkgver=1.0.1
pkgrel=4
pkgdesc="A boat racing game reminiscent of Elasto Mania and X-Moto"
arch=('i686' 'x86_64')
url="http://bloboats.dy.fi/"
license=('GPL' 'custom')
depends=('mesa' 'sdl_image' 'sdl_mixer' 'sdl_net')
source=(http://koti.mbnet.fi/makegho/c/blobtrox/$pkgname-$pkgver.tar.gz
        $pkgname.desktop $pkgname.png)
md5sums=('09d872cada575816b1047b2dfe4de40d'
         '0e909191a5fd47a2993bf193dfe0bde9'
         'd9aa6e1a6843dca44515b42f82919c32')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's#copying.txt#/usr/share/licenses/bloboats/&#' src/main.cpp
  make PREFIX='' DATADIR='$(PREFIX)/usr/share/bloboats' || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 bin/$pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.dirs "$pkgdir/etc/$pkgname.dirs"
  install -Dm644 ../$pkgname.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 copying.txt "$pkgdir/usr/share/licenses/$pkgname/copying.txt"
  cp -rf data "$pkgdir/usr/share/$pkgname"
  find "$pkgdir/usr/share/$pkgname" -type f -exec chmod 644 '{}' \;
  find "$pkgdir/usr/share/$pkgname" -type d -exec chmod 755 '{}' \;
}
