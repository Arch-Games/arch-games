# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
pkgname=bsnes
pkgver=0.072
pkgrel=1
pkgdesc="Super Nintendo Entertainment System (SNES) emulator focused on accuracy."
arch=('i686' 'x86_64')
url="http://byuu.org/bsnes/"
license=('GPL2' 'LGPL' 'zlib' 'custom:unrar')
depends=('libao' 'libgl' 'libxv' 'openal' 'sdl' 'qt>=4.5.0')
makedepends=('pkgconfig' 'mesa')
replaces=('snesfilter' 'snesreader' 'supergameboy')
source=("http://bsnes.googlecode.com/files/${pkgname}_v072.tar.bz2" "makefile.patch")

build() {
  cd "${srcdir}/${pkgname}"

  # Makefile hacks
  # Use ui-qt rather than ui-phoenix as it sports more features.
  sed -e 's|ui := ui-phoenix|ui := ui-qt|' \
      -i "${srcdir}/${pkgname}/Makefile" ||  return 1
  # Disable pulseaudio.
  sed -e 's|audio.pulseaudio ||' \
      -e 's|audio.pulseaudiosimple ||' \
      -i "${srcdir}/${pkgname}/ui-qt/Makefile" || return 1
  # Don't run gconftool
  sed -e 's|gconftool-2 --type bool|#gconftool-2 --type bool|' \
      -i "${srcdir}/${pkgname}/Makefile" || return 1

  patch -Np1 -i $srcdir/makefile.patch

  # Build the different profiles and install them
  for profile in compatibility performance accuracy; do
    msg "Building profile: ${profile}"
    make clean
    make compiler=gcc platform=x profile=${profile} || return 1
    make install profile=${profile} DESTDIR=${pkgdir} prefix=/usr || return 1
    mv ${pkgdir}/usr/bin/bsnes ${pkgdir}/usr/bin/bsnes-${profile}
  done

  # Build and install the launcher
  msg "Building launcher"
  cd "${srcdir}/${pkgname}/launcher"
  ./cc.sh
  install -m755 ${srcdir}/${pkgname}/out/${pkgname} ${pkgdir}/usr/bin/

  ##
  # Now lets build and install the plugins, you can remove any of the below if
  # you don't want/need them.
  ##
  # snesfilter :: output filters
  ##
  msg "Building plugin: snesfilter"
  cd "${srcdir}/snesfilter"
  sed -e 's|\(libsnesfilter.*\) $(DESTDIR)$(prefix)/lib|\1 $(DESTDIR)$(prefix)/lib/\1|' \
      -i "${srcdir}/snesfilter/Makefile" || return 1
  make compiler=gcc || return 1
  make install DESTDIR=${pkgdir} prefix=/usr || return 1
  chmod 644 "${pkgdir}/usr/lib/libsnesfilter.a" || return 1
  ##
  # snesreader :: handle compressed files
  ##
  msg "Building plugin: snesreader"
  cd "${srcdir}/snesreader"
  sed -e 's|\(libsnesreader.*\) $(DESTDIR)$(prefix)/lib|\1 $(DESTDIR)$(prefix)/lib/\1|' \
      -i "${srcdir}/snesreader/Makefile" || return 1
  make compiler=gcc || return 1
  make install DESTDIR=${pkgdir} prefix=/usr || return 1
  chmod 644 "${pkgdir}/usr/lib/libsnesreader.a" || return 1
  # Licenses
  mkdir -p "${pkgdir}/usr/share/licenses/bsnes"
  install -m644 "${srcdir}/snesreader/7z_C/lzma.txt" "${pkgdir}/usr/share/licenses/bsnes/license-7zip.txt"
  install -m644 "${srcdir}/snesreader/unrar/license.txt" "${pkgdir}/usr/share/licenses/bsnes/license-unrar.txt"
  ##
  # supergameboy :: Super Game Boy support
  ##
  msg "Building plugin: supergameboy"
  cd "${srcdir}/supergameboy"
  sed -e 's|\(libsupergameboy.*\) $(DESTDIR)$(prefix)/lib|\1 $(DESTDIR)$(prefix)/lib/\1|' \
      -i "${srcdir}/supergameboy/Makefile" || return 1
  make compiler=gcc || return 1
  make install DESTDIR=${pkgdir} prefix=/usr || return 1
  chmod 644 "${pkgdir}/usr/lib/libsupergameboy.a" || return 1
}
md5sums=('dcbd0f2a88682fb25d409a8276576983'
         'ef36a81c26f05e6ef61b754ede0c7b60')
