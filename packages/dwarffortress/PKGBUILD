# Maintainer: Daenyth <Daenyth+Arch [AT] gmail [DOT] com>
# Contributor: djnm <nmihalich [at} gmail dott com>
pkgname=dwarffortress
pkgver=v0.28.181.40d19
pkgrel=1
pkgdesc="A single-player fantasy game. You control a dwarven outpost or an adventurer in a randomly generated persistent world."
arch=(i686)
# Current FotF: http://www.bay12games.com/forum/index.php?topic=49921.0
url="http://www.bay12games.com/dwarves/"
license=('custom:dwarffortress')
depends=(gtk2 mesa sdl_image libsndfile openal)
source=(http://www.bay12games.com/dwarves/df_28_181_40d19_linux.tar.bz2
        dwarffortress http://brage.info/~svein/df-40d19-head.tar.bz2)
md5sums=('95fc220321e88a646bb60de9e5ce942a'
         'c7259bdd79e49e53e5d31a6ef12e8fae'
         '615c77340f9cc2191d595907c19e2995')

build() {
  cd $srcdir/df_linux
  install -dm755 $pkgdir/opt/
  cp -r $srcdir/df_linux/ $pkgdir/opt/

  # Yay for precompiled stuff with junk permissions! :D
  find $pkgdir/opt/df_linux -type d -exec chmod 755 {} +
  find $pkgdir/opt/df_linux -type f -exec chmod 644 {} +

  # My launcher is better than their launcher :D
  install -Dm755 $srcdir/dwarffortress $pkgdir/usr/bin/dwarffortress

  chmod 755 $pkgdir/opt/df_linux/dwarfort.exe

  # This probably is overkill, but I don't know what specific files df needs permission for in here.
  chown -R root:games $pkgdir/opt/df_linux/data
  find $pkgdir/opt/df_linux/data -type d -exec chmod 775 {} +
  find $pkgdir/opt/df_linux/data -type f -exec chmod 664 {} +

  install -Dm644 $srcdir/df_linux/readme.txt $pkgdir/usr/share/licenses/dwarffortress/readme.txt
}
# vim:set ts=2 sw=2 et:
