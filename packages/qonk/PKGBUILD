# Contributor: Troels Kofoed Jacobsen <tkjacobsen@gmail.com>

pkgname=qonk
pkgver=0.3.1
pkgrel=2
pkgdesc="A small space build-and-conquer strategy game."
url="http://qonk.sf.net"
arch=('i686' 'x86_64')
license=('GPL')
depends=('sdl_ttf' 'sdl_gfx' 'sdl_image')
source=(http://downloads.sourceforge.net/qonk/$pkgname-$pkgver.tar.gz
        qonk.desktop
        qonk.patch
        qonk.png)
     
build() { 
  cd ${srcdir}/${pkgname}-${pkgver}
  
  patch -Np0 -i ${srcdir}/${pkgname}.patch
  
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  make DESTDIR="${pkgdir}/" install
  
  # delete the guichan header file
  rm -r ${pkgdir}/guichan

  # install the .desktop file
  install -Dm0644 ${srcdir}/qonk.desktop ${pkgdir}/usr/share/applications/qonk.desktop
  install -Dm0644 ${srcdir}/qonk.png ${pkgdir}/usr/share/qonk/qonk.png
}
md5sums=('9491980477ac5beb5bba6b8234d2ddfd'
         '3bcea57577ee185fe2a27e3548cb6be6'
         '5e41f4e7efb6281655db29a541cf2b35'
         '8f8d7b7d06111b102f6c1ad1aad290c0')
