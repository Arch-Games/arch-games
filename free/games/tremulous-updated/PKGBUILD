# Contributor: Daenyth

pkgname=tremulous-updated
pkgver=1.1.0
pkgrel=1
pkgdesc="A free team based FPS/RTS hybrid built on the ioq3 engine. Includes community updates."
url='http://tremulous.net'
arch=('i686' 'x86_64')
license=('GPL')
depends=('sdl' 'openal')
provides=('tremulous=1.1.0')
conflicts=('tremulous')
replaces=(trem-backport)

source=(http://downloads.sourceforge.net/sourceforge/tremulous/tremulous-1.1.0.zip 
		http://releases.mercenariesguild.net/client/mg-client-manual.txt
		http://releases.mercenariesguild.net/client/mgclient_source_1.0.tar.gz
		http://releases.mercenariesguild.net/tremded/mg_tremded_source_1.01.tar.gz
		game.qvm::http://patches.mercenariesguild.net/index.php?getfile=816
		lakitu7_qvm.txt::http://patches.mercenariesguild.net/index.php?getfile=812
		tremdedrc 
		tremded.launcher 
		tremulous.launcher
		tremulous.desktop)
	
noextract=(mg_tremded_source_1.01.tar.gz 
		   mgclient_source_1.0.tar.gz)
		   
md5sums=('3df5f7565571fb9524656308347bce1b'
         '54dc412c71ee30d801d3ef53bfa89f7d'
         'a17a2b8f2d3fd9f0d136fc357e285b86'
         '938bdf944dff667b74e2132a87a49780'
         '5deb3bc2182aa22e121547b8af3d8fdb'
         'da75a281104e79e16ebfef69b415cecb'
		 'f0056120d0192a0d4d591d1114439c52'
         'b1d2ea80b05202588d93d8b679d40278'
         '131a103e63e52e5eb9e7b7f177f4c715'
		 'aef37632a2edcf74a53503a49530bad2')

build() {

	#Build and install the server
	cd $srcdir
	install -d -m755 tremded
	bsdtar -x -C tremded -f mg_tremded_source_1.01.tar.gz
	cd tremded
	make || return 1
	install -D -m644 $srcdir/tremdedrc $pkgdir/etc/tremdedrc
	install -D -m644 $srcdir/game.qvm $pkgdir/opt/tremulous/base/vm/game.qvm
	install -D -m755 build/release-linux-x86/tremded.* $pkgdir/opt/tremulous/bin/tremded

	#Build and install the client
	cd $srcdir
	install -d -m755 mgclient
	bsdtar -x -C mgclient -f mgclient_source_1.0.tar.gz
	cd mgclient
	make || return 1
	install -D -m755 build/release-linux-x86/tremulous.* $pkgdir/opt/tremulous/bin/tremulous

	#Install the packages data
	cd $srcdir/tremulous/base
	install -d -m775 -g games $pkgdir/opt/tremulous/base
	install -D -m644 *.pk3 *.cfg $pkgdir/opt/tremulous/base
	
	#Install the docs
	install -D -m644 $srcdir/mg-client-manual.txt $pkgdir/usr/share/tremulous/mg-client-manual.txt
	install -D -m644 $srcdir/lakitu7_qvm.txt $pkgdir/usr/share/tremulous/lakitu7_qvm.txt
	
	#Install the .desktop file and icons
	install -D -m644 $srcdir/misc/tremulous.{ico,xpm} $pkgdir/usr/share/pixmaps/
	install -D -m644 $srcdir/tremulous.desktop $pkgdir/usr/share/applications

	#Install the bin files
	install -D -m755 $srcdir/tremulous.launcher $pkgdir/usr/bin/tremulous
	install -D -m755 $srcdir/tremded.launcher $pkgdir/usr/bin/tremded
	
}
