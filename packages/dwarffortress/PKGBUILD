# Maintainer: Daenyth <Daenyth+Arch [AT] gmail [DOT] com>
# Contributor: djnm <nmihalich [at} gmail dott com>
pkgname=dwarffortress
pkgver=0.31.04
pkgrel=2
pkgdesc="A single-player fantasy game. You control a dwarven outpost or an adventurer in a randomly generated persistent world."
arch=(i686)
# WIP Thread: http://www.bay12forums.com/smf/index.php?topic=57492.0
url="http://www.bay12games.com/dwarves/"
install="$pkgname.install"
license=('custom:dwarffortress')
depends=(gtk2 mesa sdl_image libsndfile openal)
source=(http://www.bay12games.com/dwarves/df_31_04_linux.tar.bz2
        dwarffortress dwarffortress.desktop dwarffortress.png)
md5sums=('be5419d50fe86213c66e87c0cd52fbdb'
         '4b8812f5816f901af6a06ae8dfc01ef6'
         'c8984d1eea6e409ecf339d6ee9e91e42'
         'b1d51f82400073af9bb179e34a9209d0')

build() {
  cd $srcdir/df_linux
  install -dm755 $pkgdir/opt/
  cp -r $srcdir/df_linux/ $pkgdir/opt/

  # Yay for precompiled stuff with junk permissions! :D
  find $pkgdir/opt/df_linux -type d -exec chmod 755 {} +
  find $pkgdir/opt/df_linux -type f -exec chmod 644 {} +

  # My launcher is better than their launcher :D
  install -Dm755 $srcdir/dwarffortress $pkgdir/usr/bin/dwarffortress

  chmod 755 $pkgdir/opt/df_linux/dwarfort.exe

  # This probably is overkill, but I don't know what specific files df needs permission for in here.
  chown -R root:games $pkgdir/opt/df_linux/data
  find $pkgdir/opt/df_linux/data -type d -exec chmod 775 {} +
  find $pkgdir/opt/df_linux/data -type f -exec chmod 664 {} +

  # Desktop launcher with icon
  install -Dm644 $srcdir/dwarffortress.desktop $pkgdir/usr/share/applications/dwarffortress.desktop
  install -Dm644 $srcdir/dwarffortress.png     $pkgdir/usr/share/pixmaps/dwarffortress.png

  install -Dm644 $srcdir/df_linux/readme.txt $pkgdir/usr/share/licenses/dwarffortress/readme.txt
}
# vim:set ts=2 sw=2 et:
