# Contributor: Daenyth <Daenyth+Arch AT gmail DOT com>

pkgname=tremulous-gpp
pkgver=1
pkgrel=1
pkgdesc="The Gameplay Preview (beta) for Tremulous 1.2"
arch=(i686 x86_64)
url="http://tremulous.net"
license=('GPL')
depends=('tremulous-data=1.1.0' 'sdl' 'openal>=1.7.411')
conflicts=()
source=(http://downloads.sourceforge.net/tremulous/$pkgname$pkgver.zip
        tremulous.launcher tremded.launcher tremulous.desktop)
md5sums=('70a32d6e587e4ac67bdfd0be7d6edc2f'
         'f29398b3dabe8915ce55445a1755a67b'
         '214bda27bfad16899bab729e496b70ce'
         '7023a343ca51d438b710d4e8e804a368')

build() {
  cd "$srcdir/tremulous"

  # Unpack the sources and build
  bsdtar -xf $pkgname$pkgver-src.tar.gz || return 1
  cd "tremulous-ggp$pkgver-src/"
  make || return 1

}

package() {
  cd $srcdir/tremulous/tremulous-ggp1-src

  install -dm755 $pkgdir/opt/tremulous

  # Install Binaries
  local _arch=${CARCH/i686/x86}
  install -m755 build/release-linux-$_arch/*.$_arch $pkgdir/opt/tremulous
  for bin in $pkgdir/opt/tremulous/*.$_arch; do mv $bin ${bin/./-gpp.}; done

  # Install Client Launcher
  install -D -m755 $srcdir/tremulous.launcher $pkgdir/usr/bin/tremulous-gpp

  # Install Server Launcher
  install -D -m755 $srcdir/tremded.launcher   $pkgdir/usr/bin/tremded-gpp

  # Install Desktop Icons
  install -D -m644 $srcdir/tremulous.desktop  $pkgdir/usr/share/applications/tremulous-gpp.desktop
  install -D -m644 misc/tremulous.xpm         $pkgdir/usr/share/pixmaps/tremulous-gpp.xpm
}

# vim:set ts=2 sw=2 et:
