# Contributor: Jonathan Frazier <eyeswide@gmail.com>
# Contributor: Lisa Denia <eiffel56@gmail.com>

pkgname=openal-soft
pkgver=1.8.466
pkgrel=1
pkgdesc="a cross-platform software implementation of the OpenAL 3D audio API."
url="http://kcat.strangesoft.net/openal.html"
arch=('i686' 'x86_64')
license=('LGPL2')
makedepends=('cmake' 'alsa-lib' 'sdl')
provides=("openal=$pkgver")
conflicts=('openal')
source=(http://kcat.strangesoft.net/openal-releases/$pkgname-$pkgver.tar.bz2)
md5sums=('28d500e0a6b0a7a824b509ad9eaba7be')

build() {

  #Build and install the package
  cd "$srcdir/$pkgname-$pkgver"
  cmake . -DCMAKE_INSTALL_PREFIX:PATH=/usr || return 1
  make || return 1
  make DESTDIR="$pkgdir/" install

  #Some apps expect to have libopenal.so.0
  ln -s libopenal.so.1 $pkgdir/usr/lib/libopenal.so.0

  #Some apps expect openal_soft and not openal for pkgconfig
  ln -s openal.pc $pkgdir/usr/lib/pkgconfig/openal_soft.pc

}

# vim:set ts=2 sw=2 et:
