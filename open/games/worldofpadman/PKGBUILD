# Contributor: Tiago Pierezan Camargo <tcamargo@gmail.com
# Contributor: Paul Bredbury <brebs@sent.com>
# Contributor: Benjamin Dirks <asaru[at]wtnet[dot]de>

pkgname=worldofpadman
pkgver=1.2
pkgrel=1
pkgdesc="a free standalone online game based on the quake3 engine."
url="http://www.worldofpadman.com/"
arch=('i686' 'x86_64')
license=('GPL' 'custom')
depends=('sdl' 'libvorbis' 'curl')
makedepends=('subversion')
source=(http://thilo.kickchat.com/download/worldofpadman.run
        ftp://ftp.kickchat.com/wop/wop_patch_1_2.run
        http://thilo.kickchat.com/download/wop_padpack.zip
        wop.desktop)

md5sums=('c7650414d7865ddac26ada6b3f7b8cc9' 
         '3468fc91889795471bc68e35ea334614' 
         'c59e212b29b28e0162af33dfb118344c' 
         '4ccaa4e14b4438aec6289d1cfd4811c6')

_svntrunk=http://wop-engine.svn.sourceforge.net/svnroot/wop-engine/trunk/
_svnmod=wop-engine

build() {

  #Grab the svn wop-engine sources
  cd $srcdir
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver) || return 1
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod || return 1
  fi
  msg "SVN checkout done or server timeout"
  msg "Starting make..."
  rm -r "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #Build the engine
  /bin/sed -i "s:i386:i686:" Makefile
  make BUILD_CLIENT=1 BUILD_SERVER=1 DEFAULT_BASEDIR=/opt/worldofpadman || return 1

  #Install the executables
  cd build/release-*
  install -Dm755 wop-engine.* $pkgdir/usr/bin/worldofpadman || return 1
  install -Dm755 wopded.* $pkgdir/usr/bin/worldofpadman-ded || return 1
  
  #Data dir creation
  install -d -m755 $srcdir/wop-data
  cd $srcdir/wop-data

  #Patching the binary data file (damn, gentoo guys are crazy :P)
  for _f in $srcdir/worldofpadman.run $srcdir/wop_patch_1_2.run ; do
    _skip_lines=$(grep -a offset=.*head.*wc ${_f} | awk '{print $3}' | head -n 1)
    _skip_chars=$(head -n ${_skip_lines} $_f | wc -c)
    dd ibs=${_skip_chars} skip=1 obs=1024 conv=sync if=${_f} | tar --no-same-owner -xzf - || return 1
  done
  
  #Data extraction
  install -d $pkgdir/opt/worldofpadman/{xtras,wop}
  tar xf $srcdir/wop-data/wop-data.tar -C $pkgdir/opt/worldofpadman/wop || return 1
  tar xf $srcdir/wop-data/wop-data-1.2.tar -C $pkgdir/opt/worldofpadman/wop || return 1
  tar xf $srcdir/wop-data/extras.tar -C $pkgdir/opt/worldofpadman/xtras || return 1
  unzip -qo $srcdir/wop_padpack.zip || return 1

  #Install the .desktop and icon files
  install -Dm644 $srcdir/wop.desktop $pkgdir/usr/share/applications/worldofpadman.desktop 
  install -Dm644 $srcdir/wop-data/wop.png $pkgdir/usr/share/pixmaps/worldofpadman.png 
  
  #Install the license
  install -Dm644 $srcdir/wop-data/copyright_en.txt $pkgdir/usr/share/licenses/worldofpadman/COPYING 
  
}

# vim:set ts=2 sw=2 et:
