# Contributor: damian01w <damian01w@gmail.com>

pkgname=alienarena
pkgver=20091227
pkgrel=1
gamever=7_33
pkgdesc="Multiplayer retro sci-fi deathmatch game"
arch=('i686' 'x86_64')
url='http://icculus.org/alienarena/rpa/'
license=('GPL')
depends=('curl' 'libgl' 'libjpeg>=8' 'libxxf86dga' 'libxxf86vm' 'mesa' 'sdl' 'openal' 'libvorbis')
makedepends=('hd2u' 'unzip')
source=(http://icculus.org/alienarena/Files/alienarena${gamever}-linux${pkgver}.zip
	'alienarena.desktop' 'libjpeg8.patch')
md5sums=('d1106130fdcef6c2d4cf039c933213d7'
         '46bbe3e3a93b2e5009ffd663ad462915'
         '4dd28fac527e9bb6fcb66989fcbe3322')

 
_datadir="/usr/share/${pkgname}"
_libdir="/usr/lib/${pkgname}"
 
build() {
    cd $startdir/src/${pkgname}${gamever}
 
    # Remove pre-compiled libs
    rm {arena,data1}/game.so
 
    cd source
    # Arch isn't multilib
    sed -i -e "s:lib64:lib:" Makefile || return 1

    # Needed to compile fine with new versions of xf86dgaproto (Thanks Calimero)
    sed -i "s/xf86dga.h/Xxf86dga.h/g" unix/gl_glx.c
 
    # Fix for libjpeg8
    patch -Np0 -i $srcdir/libjpeg8.patch || return 1

    make PREFIX=/usr \
	WITH_DATADIR=yes WITH_LIBDIR=yes \
	DATADIR="${_datadir}" LIBDIR="${_libdir}" \
     || return 1
 
    #Binary

    install -D -m755 release/game.so $startdir/pkg/${_libdir}/arena/game.so || return 1
    install -D -m755 release/crx $startdir/pkg/usr/bin/${pkgname} || return 1
    install -D -m755 release/crded $startdir/pkg/usr/bin/${pkgname}-ded || return 1
 
    cd ..

    # Data files
    find arena botinfo data1 -type f | while read _f ; do
    install -D -m644 "$_f" "$startdir/pkg/$_datadir/$_f" || return 1
    done
 
    # Desktop entry
    install -D -m644 aa.png $startdir/pkg/usr/share/pixmaps/${pkgname}.png || return 1
    install -D -m644 $startdir/src/${pkgname}.desktop $startdir/pkg/usr/share/applications/${pkgname}.desktop || return 1
 
    # Docs
    cd docs
    dos2unix README.txt
    mkdir -p $startdir/pkg/usr/share/doc/$pkgname
    install -m644 -t $startdir/pkg/usr/share/doc/${pkgname}/ {license,README}.txt || return 1
}
