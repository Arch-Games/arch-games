# Contributor: Hugo Ideler <hugoideler@dse.nl>

pkgname=epiar-cvs
pkgver=20081015
pkgrel=1
pkgdesc="A space adventure/combat game written in C with SDL"
url="http://epiar.net/"
arch=('i686' 'x86_64')
license="GPL"
depends=('sdl' 'sdl_image' 'sdl_net' 'sdl_mixer' 'openal' 'mesa' 'libgl' 'libxml2' 'zlib')
makedepends=('cvs')
provides=('epiar')
conflicts=('epiar')
source=(epiar-wrapper)
md5sums=('a6bcaaa52d1a7e4e11428d4060d5ebd0')

_cvsroot=":pserver:anonymous@epiar.cvs.sourceforge.net:/cvsroot/epiar"
_cvsmod="lamprey"

build() {

	#Grab the sources with cvs
	cd $srcdir
	if [ -d $_cvsmod/CVS ]; then
		cd $_cvsmod
		cvs -z3 update -d || return 1
	else
		cvs -z3 -d $_cvsroot co -P $_cvsmod || return 1
	fi

	#Build the package
	cd $srcdir/$_cvsmod
	sh autogen.sh || return 1
	./configure || return 1
	make || return 1
	
	#Manually install the package
	install -D -m755 $srcdir/epiar-wrapper $pkgdir/usr/bin/epiar
	install -d -m755 $pkgdir/usr/share/epiar
	cp -t $pkgdir/usr/share/epiar/ *.xml *.png *.af *.dat *.ani
	chmod 644 $pkgdir/usr/share/epiar/*
	install -D -m755 Epiar $pkgdir/usr/share/epiar/

}
