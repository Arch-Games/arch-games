# Contributor: grimi <grimi@poczta.fm>

pkgname=urbanterror
pkgver=4.1
pkgrel=6
pkgdesc="Urban Terror is a team-based tactical shooter based on the Quake 3 Engine (Stand-Alone Version)"
arch=('i686' 'x86_64')
url="http://www.urbanterror.net"
license=('freeware')
depends=('sdl' 'openal' 'curl')
makedepends=('unzip' 'mesa')
source=("ftp://ftp.snt.utwente.nl/pub/games/urbanterror/UrbanTerror_41_FULL.zip" \
	"http://ftp.snt.utwente.nl/pub/games/urbanterror/iourbanterror/source/complete/ioUrbanTerrorSource_2007_12_20.zip" \
	"cmd.c.patch" "g_cmds.c.patch" "qcommon.h.patch" "sv_client.c.patch" "sv_main.c.patch" \
	"urbanterror.sh" "urbanterror-server.sh" "urbanterror.desktop" "urbanterror.png")
md5sums=('1370306ea236f65f595e7ca70765e469'
         '4dbe5a03a10f23138f42b5e0dc39598b'
         '0e5cc83478b8d37f59740620e5eccd21'
         '1120e5f2a3a5944f219a432e5eb9bcaa'
         'a12059253d555fdb169d2717899b47e6'
         '57241e730301a78d1b13de5b9cc9f68a'
         '8b24a2aa5446e9c6ccbc04e844b189ab'
         '434b7658f6dfb4c715eb739cf115804f'
         '46e05cc3b03d964713f7479087998d13'
         '08a99f4d7ad63024bc886e118ddcbc0f'
         'f9a57d898df73f43c6a85c8d8cc455ba')



build() {

	## compile client
	cd $srcdir/ioUrbanTerrorClientSource
	make || return 1
	
	## compile server
	cd $srcdir/ioUrbanTerrorServerSource
	patch -Np1 -i $srcdir/cmd.c.patch || return 1
	patch -Np1 -i $srcdir/g_cmds.c.patch || return 1
	patch -Np1 -i $srcdir/qcommon.h.patch || return 1
	patch -Np1 -i $srcdir/sv_client.c.patch || return 1
	patch -Np1 -i $srcdir/sv_main.c.patch || return 1
	make || return 1
}


package() {

	## prepare folder
	install -d $pkgdir/opt/urbanterror/q3ut4/
	
	cd $pkgdir/opt/urbanterror
	
	# copy execs
	install -m755 $srcdir/ioUrbanTerrorClientSource/build/release-linux-*/ioUrbanTerror.* ioUrbanTerror
	install -m755 $srcdir/ioUrbanTerrorServerSource/build/release-linux-*/ioUrTded.* ioUrTded

	## copy base UT files	
	#install -m644 $srcdir/UrbanTerror/q3ut4/*.{cfg,doc,ico,pk3,txt} q3ut4/
	for i in $srcdir/UrbanTerror/q3ut4/*.{cfg,doc,ico,pk3,txt}; do
	    mv "$i" q3ut4/
	    chmod 644 "q3ut4/$(basename "$i")"
	done

	## copy docs
	install -m644 $srcdir/UrbanTerror/ioUrbanTerror_id-readme.txt ./
	install -m644 $srcdir/UrbanTerror/ioUrbanTerror_COPYING.txt ./
	install -m644 $srcdir/UrbanTerror/ioUrbanTerror_README.txt ./

	## desktop launcher
	install -Dm644 $srcdir/urbanterror.desktop $pkgdir/usr/share/applications/urbanterror.desktop
	install -Dm644 $srcdir/urbanterror.png $pkgdir/usr/share/pixmaps/urbanterror.png

	## launch scripts
	install -Dm755 $srcdir/urbanterror.sh $pkgdir/usr/bin/urbanterror
	install -Dm755 $srcdir/urbanterror-server.sh $pkgdir/usr/bin/urbanterror-server
}

