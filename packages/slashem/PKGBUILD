# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: wizzomafizzo <wizzomafizzo@gmail.com>

pkgname=slashem
pkgver=0.0.7E7F3
pkgrel=2
pkgdesc='Nethack variant Super Lotsa Added Stuff Hack - Extended Magic'
arch=('i686' 'x86_64')
url="http://www.slashem.org/"
license=('custom')
depends=('ncurses' 'gzip')
options=('!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/se007e7f3.tar.gz)

md5sums=('54b4534fe85f08722e8b6b38d52c2e9a')

build(){
  cd ${srcdir}/$pkgname-$pkgver/sys/unix
  sh setup.sh

  cd ${srcdir}/$pkgname-$pkgver/include
  sed -e '/define COMPRESS/ s|/usr/bin/compress|/bin/gzip|' \
      -e '/define COMPRESS_EXTENSION/ s|".Z"|".gz"|' \
      -e 's|^/\* \(#define DLB\) \*/|\1|' -i config.h
  sed -e 's|^/\* \(#define LINUX\) \*/|\1|' \
      -e 's|^/\* \(#define TIMED_DELAY\) \*/|\1|' -i unixconf.h
  cd ${srcdir}/$pkgname-$pkgver/src
  sed -e 's|^# \(WINTTYLIB = -lncurses\)|\1|' \
      -e 's|^WINTTYLIB = -ltermlib|#&|' -i Makefile
  cd ..
  sed -e '/^GAMEUID\s*=/ s|games|root|' \
      -e '/^GAMEGRP\s*=/ s|bin|root|' \
      -e '/^SHELLDIR\s*=/ s|/games|/bin|' \
      -e '/GAMEDIR/ s|/local/slashemdir|/share/slashem|' \
      -e '/SHELLDIR/ s|/local/bin|/bin|' -i Makefile

  make || return 1
  make PREFIX=${pkgdir}/usr install
  sed -e 's|HACKDIR=.*/pkg/usr/|HACKDIR=/usr/|' \
      -i ${pkgdir}/usr/bin/slashem
  install -Dm644 dat/license ${pkgdir}/usr/share/licenses/$pkgname/license
}
