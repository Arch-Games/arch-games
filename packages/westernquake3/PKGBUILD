# Contributor: H.GÃ¶khan SARI <hsa2@difuzyon.net>

pkgname=westernquake3
pkgver=2.2_beta1
pkgrel=3
pkgdesc="A Western game using Quake3 engine."
url="http://www.smokin-guns.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('sdl' 'openal' 'curl')
makedepends=('subversion' 'unzip')
source=(http://bootblock.de/wq3/wq3_b22_full.zip
        westernquake3.png
        westernquake3.desktop
        westernquake3.sh
        westernq3ded.sh)
    
md5sums=('6376bab9970bf4c22d40e41019a1900f'
         '9f02379e5f911b16760f28b74d9f321b'
         '8cb85c47fc906acb644521d6561bad0c'
         '2a5cfdf6f6e559a0cd62c071362cd57a'
         'fb9283a2d2e0528070934315e9c9c236')

_svntrunk=svn://svn.icculus.org/quake3/trunk
_svnmod=ioquake3

build() {

  #Grab the svn ioquake3 sources
  cd $srcdir
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up) || return 1
  else
    svn co $_svntrunk --config-dir ./ $_svnmod || return 1
  fi
  msg "SVN checkout done or server timeout"
  msg "Starting make..."
  rm -r $srcdir/$_svnmod-build
  cp -r $srcdir/$_svnmod $srcdir/$_svnmod-build
  cd $srcdir/$_svnmod-build

  #Build the ioquake engine
  sed -i "s:/usr/local/games/quake3:$pkgdir/opt/westernquake3:" Makefile
  make || return 1
  make copyfiles || return 1 
  
  #Fix the stupid naming conversion
  mv $pkgdir/opt/westernquake3/ioquake3* $pkgdir/opt/westernquake3/ioquake3
  mv $pkgdir/opt/westernquake3/ioq3ded* $pkgdir/opt/westernquake3/ioq3ded

  #Install the data files
  cd $srcdir/westernq3
  install -d -m755 $pkgdir/opt/westernquake3/westernq3 $pkgdir/usr/bin $pkgdir/usr/share/pixmaps/ $pkgdir/usr/share/applications/
  install -D -m644 *.pk3 $pkgdir/opt/westernquake3/westernq3/
  install -D -m644 *.cfg $pkgdir/opt/westernquake3/westernq3/
  
  #Install the binary files
  install -m755 $srcdir/westernquake3.sh $pkgdir/usr/bin/westernquake3
  install -m755 $srcdir/westernq3ded.sh $pkgdir/usr/bin/westernq3ded
  
  #Install the desktop file and the icon
  install -m644 $srcdir/westernquake3.png $pkgdir/usr/share/pixmaps/
  install -m644 $srcdir/westernquake3.desktop $pkgdir/usr/share/applications/

}

# vim:set ts=2 sw=2 et:
