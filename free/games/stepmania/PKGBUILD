# Contributor: August Gustavsson <august.g@telia.com>
# Contributor: Travis Nickles <ryoohki7@yahoo.com>
# Contributor: Joonas Niilola <juippis@roskakori.org>

pkgname=stepmania
pkgver=3.9
pkgrel=14
pkgdesc="An Advanced Dance Simulation Game"
url="http://www.stepmania.com/"
arch=('i686' 'x86_64')
license=('MIT')
depends=('ffmpeg' 'gtk2' 'libmad' 'lua' 'mesa')
makedepends=('pkgconfig' 'make' 'patch' 'autoconf' 'automake')
install=stepmania.install
source=(http://downloads.sourceforge.net/$pkgname/StepMania-$pkgver-linux.tar.gz 
        http://downloads.sourceforge.net/$pkgname/StepMania-$pkgver-src.tar.gz 
        stepmania.patch 
        stepmania.desktop 
        stepmania.sh 
        stepmania.xpm)

md5sums=('c786dd30170e2770b6b1638f1115472b'
     '28bbbc985788bc990fa7042e2d7320b8'
     '477c3cfe5a9fdbe36ff468b9f3dc8f5d'
     '8fb95933764b52ab1fe6a099c779035e'
     '1cc95d5c33c9fac516ff1912be7e63c9'
     '10732965e8abf53ba4b85c63377daf2e')

build() {

  #Patch, build and install the package
  cd $srcdir/StepMania-$pkgver-src
  patch -Np1 -i $srcdir/stepmania.patch  || return 1
  aclocal -I autoconf/m4
  autoconf
  automake
  ./configure --enable-force-oss
  make || return 1
  make bindir=/opt/stepmania DESTDIR=$pkgdir install

  #Install the package data
  install -d $pkgdir/opt/stepmania/Cache/{Banners,Songs} $pkgdir/opt/stepmania/Data/LocalProfiles $pkgdir/opt/stepmania/Data/MachineProfile/{Edits,Screenshots}
  rm $srcdir/StepMania-$pkgver/{GtkModule.so,stepmania}
  cp -r $srcdir/StepMania-$pkgver/* $pkgdir/opt/stepmania/
  touch $pkgdir/opt/stepmania/{info,log}.txt
  rm -f $pkgdir/opt/stepmania/Data/MachineProfile/Stats.xml

  #Change file permissions so that users in the games group can write save data, write to log files, and add more content
  cd $pkgdir/opt/stepmania
  chown -R root:games *
  chmod 2775 {Announcers,BGAnimations,Cache,CDTitles,Characters,Courses,Data,Docs,NoteSkins,RandomMovies,Songs,Themes,Visualizations}
  chmod 2775 Cache/{Banners,Songs}
  chmod 664 {info,log}.txt
  cd Data
  chmod 664 {AI.ini,Translation.dat,Unlocks.dat,splash.png}
  chmod 2775 LocalProfiles MachineProfile MachineProfile/{Edits,Screenshots}

  #Install the .desktop and icon files
  install -D -m644 $srcdir/stepmania.desktop $pkgdir/usr/share/applications/stepmania.desktop
  install -D -m644 $srcdir/stepmania.xpm $pkgdir/usr/share/pixmaps/stepmania.xpm
  
  #Install the license
  install -D -m644 $srcdir/StepMania-$pkgver-src/Copying.txt $pkgdir/usr/share/licenses/stepmania/Copying

  #Install the bin file
  install -D -m755 $srcdir/stepmania.sh $pkgdir/usr/bin/stepmania
  
}

# vim:set ts=2 sw=2 et:
