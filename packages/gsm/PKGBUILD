# $Id: PKGBUILD, 2010/03/6$
# Maintainer: Maxime de Roucy <maxime.deroucy@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=gsm
pkgver=1.0.13
pkgrel=3
pkgdesc="Shared libraries for GSM 06.10 lossy speech compression"
arch=('i686' 'x86_64')
url="http://www.quut.com/gsm/"
license=('custom')
depends=('glibc')
source=(http://www.quut.com/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('c1ba392ce61dc4aff1c29ea4e92f6df4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}-pl${pkgver##*.}/"

  [ "${CARCH}" = "x86_64" ] && CFLAGS="${CFLAGS} -fPIC"
  make CCFLAGS="-c ${CFLAGS}" || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}-pl${pkgver##*.}/"

  # Prepare directories
  install -d ${pkgdir}/usr/{bin,lib,include/gsm,share/man/man{1,3}}

  make INSTALL_ROOT=${pkgdir}/usr \
    GSM_INSTALL_INC=${pkgdir}/usr/include/gsm \
    GSM_INSTALL_MAN=${pkgdir}/usr/share/man/man3 \
    TOAST_INSTALL_MAN=${pkgdir}/usr/share/man/man1 \
    install

  # Install license
  install -D -m644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/license.txt

  # Replace hardlinks with symlinks
  cd "$pkgdir/usr/bin"
  rm untoast tcat
  ln -s toast untoast
  ln -s toast tcat
}

# vim:set ts=2 sw=2 et:
